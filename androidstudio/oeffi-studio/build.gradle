import java.text.SimpleDateFormat
import groovy.json.JsonSlurper

plugins {
    alias(libs.plugins.android.application)
    id 'androidsvgdrawable'
}

def now = new Date()
def timeInt = (int) (now.getTime() / 60000)
def timeFmt = new SimpleDateFormat('yyyyMMddHHmm')
timeFmt.setTimeZone(TimeZone.getTimeZone('UTC'))
def timeStr = timeFmt.format(now)
timeFmt = new SimpleDateFormat("EEE, dd MMM yyyy HH:mm:ss 'GMT'", Locale.US)
timeFmt.setTimeZone(TimeZone.getTimeZone('UTC'))
def timeModifiedStr = timeFmt.format(new Date(now.getTime() + 60000 * 5))
def githubTag = "v${timeStr}"
def gitBranch = getCurrentGitBranch()
println "gitBranch = ${gitBranch}"

def CREDENTIALS = [:]
ext {
    oeffi = [:]
}

def oeffi_ng_password = System.getenv('OEFFI_NG_JKS_PASSWORD')
if (!oeffi_ng_password) {
    file('../../passwords.properties').withInputStream {
        def props = new Properties()
        props.load(it)
        oeffi_ng_password = props.get('oeffi_ng.jks.password')
        CREDENTIALS.github_user = props.get('github.user')
        CREDENTIALS.github_repo = props.get('github.repo')
        CREDENTIALS.github_token = props.get('github.token')
    }
}

CREDENTIALS.editionName = "${gitBranch != null && gitBranch !== 'main' && gitBranch != 'master' && gitBranch != 'nextgen' ? gitBranch : null}"
CREDENTIALS.applicationId = "de.santawho.oeffi_ng${CREDENTIALS.editionName ? ".${CREDENTIALS.editionName}" : ''}"
CREDENTIALS.report_email = "oeffi.report@santawho.de"
CREDENTIALS.github_url = "https://github.com/${CREDENTIALS.github_user}/${CREDENTIALS.github_repo}"
CREDENTIALS.github_release_url = "${CREDENTIALS.github_url}/releases/latest/download"
CREDENTIALS.asset_name_apk_file = "oeffi-ng.apk"
CREDENTIALS.asset_name_manifest = "manifest.json"

project.oeffi.CREDENTIALS = CREDENTIALS

println "applicationId = ${CREDENTIALS.applicationId}"

android {
    namespace 'de.schildbach.oeffi'
    compileSdk 35

    defaultConfig {
        applicationId "de.schildbach.oeffi"

        minSdk 26
        targetSdk 35
//        versionCode 990000
//        versionName "99-handmade"
        versionCode timeInt
        versionName timeStr

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        oeffi_ng {
            storeFile file("../../oeffi-ng.jks")
            keyAlias "apk"
            storePassword oeffi_ng_password
            keyPassword oeffi_ng_password
        }
    }

    flavorDimensions = ['flavor']
    productFlavors {
        ng {
            dimension 'flavor'
            applicationId "${CREDENTIALS.applicationId}"
            // applicationIdSuffix '.ng'
            versionNameSuffix "-ng${CREDENTIALS.editionName ? "-${CREDENTIALS.editionName}" : ''}"
            resValue "string", "app_name", "Öffi NG${CREDENTIALS.editionName ? " ${CREDENTIALS.editionName}" : ''}"
            resValue "string", "about_git_url", "${CREDENTIALS.github_url}"
            resValue "string", "about_changelog_url", "${CREDENTIALS.github_url}/wiki"
            resValue "string", "about_update_apk_url", "${CREDENTIALS.github_release_url}/${CREDENTIALS.asset_name_apk_file}"
            resValue "string", "about_update_manifest_url", "${CREDENTIALS.github_release_url}/${CREDENTIALS.asset_name_manifest}"
            resValue "string", "about_update_modified", timeModifiedStr  // If-Modified-Since: xxx, 25 Feb 2025 17:10:00 GMT
            resValue "string", "error_reporter_mail_address", "${CREDENTIALS.report_email}"
            resValue "string", "app_url_host", "santawho.de"
            resValue "string", "app_url_path_prefix", "/@apps/oeffi-ng${CREDENTIALS.editionName ? "-${CREDENTIALS.editionName}" : ''}"
            resValue "string", "about_faq_url", "en~${CREDENTIALS.github_url}/wiki/faq~~de~${CREDENTIALS.github_url}/wiki/faq"
            resValue "string", "about_privacy_policy_url", "en~${CREDENTIALS.github_url}/wiki/privacy-policy~~de~${CREDENTIALS.github_url}/wiki/privacy-policy"
            resValue "string", "oeffi_base_default_url", "https://santawho.github.io/oeffi-web/"
            resValue "string", "plans_base_default_url", "https://santawho.github.io/oeffi-web/plans"
            resValue "string", "messages_base_default_url", "https://santawho.github.io/oeffi-web/messages"
            resValue "bool", "flags_show_twitter", "false"
            resValue "bool", "flags_show_navigation_drawer_footer", "false"
        }
    }

    sourceSets {
        main {
            manifest.srcFile '../../oeffi/AndroidManifest.xml'
            java.srcDirs = ['../../oeffi/src', '../../public-transport-enabler/src']
            res.srcDirs = ['../../oeffi/res']
            assets.srcDirs = ['../../oeffi/assets']
        }
        test {
            java.srcDirs = ['../../oeffi/test', '../../public-transport-enabler/test']
        }
        ng {
            res.srcDirs = ['./res']
        }
    }

    buildTypes {
        configureEach {
            minifyEnabled false
            signingConfig signingConfigs.oeffi_ng
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'

            resValue "string", "app_name", "Öffi NG${CREDENTIALS.editionName ? " ${CREDENTIALS.editionName}" : ''} Debug"
            resValue "string", "app_url_path_prefix", "/@apps/oeffi-ng${CREDENTIALS.editionName ? "-${CREDENTIALS.editionName}" : ''}-debug/"
        }
        preview {
//            minifyEnabled true
//            shrinkResources false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
//            minifyEnabled true
//            shrinkResources false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        releaseWithProguard {
//            minifyEnabled true
//            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
//    implementation libs.support.v13
//    implementation libs.preference.v7
//    implementation libs.preference.v14
    implementation libs.preferencex
    implementation libs.jsr305
    implementation libs.annotation
    implementation libs.recyclerview
    implementation libs.coordinatorlayout
    implementation libs.drawerlayout
    implementation libs.core
    implementation libs.activity
    implementation libs.core.splashscreen
    //noinspection GradleDependency
    implementation libs.okhttp
    //noinspection GradleDependency
    implementation libs.okhttp3.logging.interceptor
    implementation libs.guava
    implementation libs.osmdroid.android
    //noinspection GradleDependency
    implementation libs.slf4j.api
    implementation libs.logback.android
    implementation libs.msgpack.core
    implementation libs.swipelayout
    testImplementation libs.junit

//    implementation libs.appcompat
//    implementation libs.material
//    testImplementation libs.junit
//    androidTestImplementation libs.ext.junit
//    androidTestImplementation libs.espresso.core
}

tasks.register('svgToPng', fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = project.files('graphics')
    to = file('res')
    targetedDensities = ['xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}

afterEvaluate {
    assembleNgRelease.doLast {
        // runs always after the NG release build. Why the following ??
//        android.applicationVariants.all { variant ->
//            println "variant.buildType.name = ${variant.buildType.name}"
//            if (variant.buildType.name == 'release') {
//                def releaseBuildTask = tasks.create(name: "release") {
//                    println("....................  test   ..............................")
//                }
//                releaseBuildTask.mustRunAfter variant.assembleProvider
//            }
//        }
        createRelease(githubTag, new File("${project.projectDir}/build/outputs/apk/ng/release/oeffi-studio-ng-release.apk"))
    }
}

def githubCreateRelease(String tagName) {
    def conn = ((HttpURLConnection) new URL("https://api.github.com/repos/${project.oeffi.CREDENTIALS.github_user}/${project.oeffi.CREDENTIALS.github_repo}/releases").openConnection())
    conn.requestMethod = 'POST'
    conn.setRequestProperty('Accept', 'application/vnd.github+json')
    conn.setRequestProperty('Authorization', "Bearer ${project.oeffi.CREDENTIALS.github_token}")
    conn.setRequestProperty('X-GitHub-Api-Version', '2022-11-28')
    conn.doOutput = true
    conn.outputStream.write("{\"tag_name\":\"${tagName}\"}".getBytes('UTF-8'))
    def respCode = conn.getResponseCode();
    def respBody = conn.getInputStream().getText()
    println("create release: ${respCode}: ${respBody}");
    if (!respCode.equals(201))
        throw respBody
    def resp = (Map) new JsonSlurper().parseText(respBody)
    return resp.id.toString();
}

def uploadToGithub(String releaseId, String assetName, byte[] uploadBytes) {
    def uploadUrl = "https://uploads.github.com/repos/${project.oeffi.CREDENTIALS.github_user}/${project.oeffi.CREDENTIALS.github_repo}/releases/${releaseId}/assets?name=${assetName}"
    println "upload to ${uploadUrl}"
    def conn = ((HttpURLConnection) new URL(uploadUrl).openConnection())
    conn.requestMethod = 'POST'
    conn.setRequestProperty('Content-Type', 'application/octet-stream')
    conn.setRequestProperty('Accept', 'application/vnd.github+json')
    conn.setRequestProperty('Authorization', "Bearer ${project.oeffi.CREDENTIALS.github_token}")
    conn.setRequestProperty('X-GitHub-Api-Version', '2022-11-28')
    conn.doOutput = true
    conn.outputStream.write(uploadBytes)
    def respCode = conn.getResponseCode()
    def respBody = conn.getInputStream().getText()
    println("upload ${assetName}: ${respCode}: ${respBody}")
    if (!respCode.equals(201))
        throw respBody
}

def createRelease(String tagName, File apkFile) {
    println "Creating release ${tagName} from ${apkFile.absolutePath}"
    if (!apkFile.exists()) {
        println "${apkFile.absolutePath} does not exist"
        throw "${apkFile.absolutePath} does not exist";
    }
    "git tag ${tagName}".execute()
    "git push --tags".execute()

    def manifestJson = "{\"tag\": \"${tagName}\"}"

    def releaseId = githubCreateRelease(tagName)
    uploadToGithub(releaseId, project.oeffi.CREDENTIALS.asset_name_apk_file, apkFile.readBytes())
    uploadToGithub(releaseId, project.oeffi.CREDENTIALS.asset_name_manifest, manifestJson.getBytes("UTF-8"))
}

def getCurrentGitBranch() {
    def gitBranch = null
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            gitBranch = result.text.trim()
        }
    } catch (e) {
    }
    return gitBranch
}
