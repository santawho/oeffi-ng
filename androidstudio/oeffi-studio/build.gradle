import java.text.SimpleDateFormat
import groovy.json.JsonSlurper

plugins {
    alias(libs.plugins.android.application)
    id 'androidsvgdrawable'
}

def now = new Date()
def timeInt = (int) (now.getTime() / 60000)
def timeFmt = new SimpleDateFormat('yyyyMMddHHmm')
timeFmt.setTimeZone(TimeZone.getTimeZone('UTC'))
def timeStr = timeFmt.format(now)
timeFmt = new SimpleDateFormat("EEE, dd MMM yyyy HH:mm:ss 'GMT'", Locale.US)
timeFmt.setTimeZone(TimeZone.getTimeZone('UTC'))
def timeModifiedStr = timeFmt.format(new Date(now.getTime() + 60000 * 5))
def githubTag = "v${timeStr}"

def CREDENTIALS = [:]
ext {
    oeffi = [:]
}

def oeffi_ng_password = System.getenv('OEFFI_NG_JKS_PASSWORD')
if (!oeffi_ng_password) {
    file('../../passwords.properties').withInputStream {
        def props = new Properties()
        props.load(it)
        oeffi_ng_password = props.get('oeffi_ng.jks.password')
        CREDENTIALS.github_user = props.get('github.user')
        CREDENTIALS.github_repo = props.get('github.repo')
        CREDENTIALS.github_token = props.get('github.token')
    }
}

CREDENTIALS.report_email = "oeffi.report@santawho.de"
CREDENTIALS.github_url = "https://github.com/${CREDENTIALS.github_user}/${CREDENTIALS.github_repo}"
CREDENTIALS.github_release_url = "${CREDENTIALS.github_url}/releases/latest/download"
CREDENTIALS.asset_name_apk_file = "oeffi-ng.apk"
CREDENTIALS.asset_name_manifest = "manifest.json"

project.oeffi.CREDENTIALS = CREDENTIALS


android {
    namespace 'de.schildbach.oeffi'
    compileSdk 35

    defaultConfig {
        applicationId "de.schildbach.oeffi"

        minSdk 24
        targetSdk 35
//        versionCode 990000
//        versionName "99-handmade"
        versionCode timeInt
        versionName timeStr

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        oeffi_ng {
            storeFile file("../../oeffi-ng.jks")
            keyAlias "apk"
            storePassword oeffi_ng_password
            keyPassword oeffi_ng_password
        }
    }

    flavorDimensions = ['flavor']
    productFlavors {
        ng {
            dimension 'flavor'
            applicationId "de.santawho.oeffi_ng"
            // applicationIdSuffix '.ng'
            versionNameSuffix '-ng'
            resValue "string", "app_name", "Ã–ffi NG"
            resValue "string", "about_git_url", "${CREDENTIALS.github_url}"
            resValue "string", "about_changelog_url", "${CREDENTIALS.github_url}/wiki"
            resValue "string", "about_update_apk_url", "${CREDENTIALS.github_release_url}/${CREDENTIALS.asset_name_apk_file}"
            resValue "string", "about_update_manifest_url", "${CREDENTIALS.github_release_url}/${CREDENTIALS.asset_name_manifest}"
            resValue "string", "about_update_modified", timeModifiedStr  // If-Modified-Since: xxx, 25 Feb 2025 17:10:00 GMT
            resValue "string", "error_reporter_mail_address", "${CREDENTIALS.report_email}"
            resValue "string", "app_url_host", "santawho.de"
            resValue "string", "app_url_path_prefix", "/@apps/oeffi-ng/"
        }
    }

    sourceSets {
        main {
            manifest.srcFile '../../oeffi/AndroidManifest.xml'
            java.srcDirs = ['../../oeffi/src', '../../public-transport-enabler/src']
            res.srcDirs = ['../../oeffi/res']
            assets.srcDirs = ['../../oeffi/assets']
        }
        test {
            java.srcDirs = ['../../oeffi/test']
        }
        ng {
            res.srcDirs = ['./res']
        }
    }

    buildTypes {
        configureEach {
            minifyEnabled false
            signingConfig signingConfigs.oeffi_ng
        }
        debug {
            debuggable true
//            applicationIdSuffix '.debug'
        }
        preview {
//            minifyEnabled true
//            shrinkResources false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
//            minifyEnabled true
//            shrinkResources false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        releaseWithProguard {
//            minifyEnabled true
//            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.core:core:1.15.0'
    implementation 'androidx.activity:activity:1.9.3'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.13'
    implementation 'com.google.guava:guava:33.3.0-android'
    implementation 'org.osmdroid:osmdroid-android:6.1.20'
    //noinspection GradleDependency
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'org.msgpack:msgpack-core:0.9.9'
    testImplementation 'junit:junit:4.13.2'

//    implementation libs.appcompat
//    implementation libs.material
//    testImplementation libs.junit
//    androidTestImplementation libs.ext.junit
//    androidTestImplementation libs.espresso.core
}

tasks.register('svgToPng', fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = project.files('graphics')
    to = file('res')
    targetedDensities = ['xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}

afterEvaluate {
    assembleNgRelease.doLast {
        // runs always afetr the NG release build. Why the following ??
//        android.applicationVariants.all { variant ->
//            println "variant.buildType.name = ${variant.buildType.name}"
//            if (variant.buildType.name == 'release') {
//                def releaseBuildTask = tasks.create(name: "release") {
//                    println("....................  test   ..............................")
//                }
//                releaseBuildTask.mustRunAfter variant.assembleProvider
//            }
//        }
        createRelease(githubTag, new File("${project.projectDir}/build/outputs/apk/ng/release/oeffi-studio-ng-release.apk"))
    }
}

def githubCreateRelease(String tagName) {
    def conn = ((HttpURLConnection) new URL("https://api.github.com/repos/${project.oeffi.CREDENTIALS.github_user}/${project.oeffi.CREDENTIALS.github_repo}/releases").openConnection())
    conn.requestMethod = 'POST'
    conn.setRequestProperty('Accept', 'application/vnd.github+json')
    conn.setRequestProperty('Authorization', "Bearer ${project.oeffi.CREDENTIALS.github_token}")
    conn.setRequestProperty('X-GitHub-Api-Version', '2022-11-28')
    conn.doOutput = true
    conn.outputStream.write("{\"tag_name\":\"${tagName}\"}".getBytes('UTF-8'))
    def respCode = conn.getResponseCode();
    def respBody = conn.getInputStream().getText()
    println("create release: ${respCode}: ${respBody}");
    if (!respCode.equals(201))
        throw respBody
    def resp = (Map) new JsonSlurper().parseText(respBody)
    return resp.id.toString();
}

def uploadToGithub(String releaseId, String assetName, byte[] uploadBytes) {
    def uploadUrl = "https://uploads.github.com/repos/${project.oeffi.CREDENTIALS.github_user}/${project.oeffi.CREDENTIALS.github_repo}/releases/${releaseId}/assets?name=${assetName}"
    println "upload to ${uploadUrl}"
    def conn = ((HttpURLConnection) new URL(uploadUrl).openConnection())
    conn.requestMethod = 'POST'
    conn.setRequestProperty('Content-Type', 'application/octet-stream')
    conn.setRequestProperty('Accept', 'application/vnd.github+json')
    conn.setRequestProperty('Authorization', "Bearer ${project.oeffi.CREDENTIALS.github_token}")
    conn.setRequestProperty('X-GitHub-Api-Version', '2022-11-28')
    conn.doOutput = true
    conn.outputStream.write(uploadBytes);
    def respCode = conn.getResponseCode();
    def respBody = conn.getInputStream().getText()
    println("upload ${assetName}: ${respCode}: ${respBody}");
    if (!respCode.equals(201))
        throw respBody
}

def createRelease(String tagName, File apkFile) {
    println "Creating release ${tagName} from ${apkFile.absolutePath}"
    if (!apkFile.exists()) {
        println "${apkFile.absolutePath} does not exist"
        throw "${apkFile.absolutePath} does not exist";
    }
    "git tag ${tagName}".execute()
    "git push --tags".execute()

    def manifestJson = "{\"tag\": \"${tagName}\"}"

    def releaseId = githubCreateRelease(tagName);
    uploadToGithub(releaseId, project.oeffi.CREDENTIALS.asset_name_apk_file, apkFile.readBytes())
    uploadToGithub(releaseId, project.oeffi.CREDENTIALS.asset_name_manifest, manifestJson.getBytes("UTF-8"))
}
