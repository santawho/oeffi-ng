import java.text.SimpleDateFormat

plugins {
    alias(libs.plugins.android.application)
    id 'androidsvgdrawable'
}

def now = new Date()
def timeStr = new SimpleDateFormat('yyyyMMddHHmm').format(now)
def timeInt = (int) (now.getTime() / 60000)

android {
    namespace 'de.schildbach.oeffi'
    compileSdk 35

    defaultConfig {
        applicationId "de.schildbach.oeffi"

        minSdk 24
        targetSdk 35
//        versionCode 990000
//        versionName "99-handmade"
        versionCode timeInt
        versionName timeStr

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions = ['flavor']
    productFlavors {
        ng {
            dimension 'flavor'
            versionNameSuffix '-ng'
            applicationIdSuffix '.ng'
            resValue "string", "app_name", "Ã–ffi NG"
        }
    }

    sourceSets {
        main {
            manifest.srcFile '../../oeffi/AndroidManifest.xml'
            java.srcDirs = ['../../oeffi/src', '../../public-transport-enabler/src']
            res.srcDirs = ['../../oeffi/res']
            assets.srcDirs = ['../../oeffi/assets']
        }
        test {
            java.srcDirs = ['../../oeffi/test']
        }
        ng {
            res.srcDirs = ['./res']
        }
    }

    buildTypes {
        all {
            minifyEnabled false
        }
        debug {
            debuggable true
        }
        release {
//            minifyEnabled true
//            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.core:core:1.15.0'
    implementation 'androidx.activity:activity:1.9.3'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.13'
    implementation 'com.google.guava:guava:33.3.0-android'
    implementation 'org.osmdroid:osmdroid-android:6.1.20'
    //noinspection GradleDependency
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'com.github.tony19:logback-android:3.0.0'
    testImplementation 'junit:junit:4.13.2'

//    implementation libs.appcompat
//    implementation libs.material
//    testImplementation libs.junit
//    androidTestImplementation libs.ext.junit
//    androidTestImplementation libs.espresso.core
}

tasks.register('svgToPng', fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = project.files('graphics')
    to = file('res')
    targetedDensities = ['xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}
